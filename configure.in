dnl Process this file with autoconf to produce a configure script.
AC_INIT(NEWS)
AM_INIT_AUTOMAKE(lcdf-typetools, 0.7)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
if test -n "$GCC" ; then CC="$CC -W -Wall" ; CXX="$CXX -W -Wall" ; fi

AC_PROG_RANLIB


dnl
dnl directory searching
dnl

AC_HEADER_DIRENT


dnl
dnl kpathsea, cfftota
dnl

AC_ARG_WITH(kpathsea, [  --with-kpathsea=PREFIX  Kpathsea programs are under PREFIX [[$PATH]]],
  [kpathsea=$withval], [kpathsea=])

if test "x$kpathsea" = xno; then
    :
else
    AC_DEFINE(HAVE_KPATHSEA, 1, [Define if you want to use kpathsea.])
    if test "x$kpathsea" = x -o "x$kpathsea" = xyes; then
	AC_CHECK_PROG([KPSEWHICH], [kpsewhich], [yes], [no])
	AC_DEFINE(KPATHSEA_BINDIR, [""], [The prefix to prepend to kpathsea programs.])
    else
	kpathsea=`echo kpathsea | sed 's/\/*$//'`
	AC_CHECK_PROG([KPSEWHICH], [kpsewhich], [yes], [no], [$kpathsea/bin])
	AC_DEFINE(KPATHSEA_BINDIR, ["$kpathsea/bin/"])
    fi
    if test $KPSEWHICH = no; then
	AC_MSG_ERROR([
=========================================

I can't find the 'kpsewhich' program. Tell me where to look with the
--with-kpathsea=PREFIX option, or disable support with --without-kpathsea.

=========================================])
    fi
fi

AC_ARG_ENABLE(cfftot1, [  --disable-cfftot1       disable running cfftot1 from otftotfm], :, enable_cfftot1=yes)

if test "x$enable_cfftot1" = xyes; then
    AC_DEFINE(HAVE_AUTO_CFFTOT1, 1, [Define to run cfftot1 automatically from otftotfm.])
fi


dnl
dnl <new> and/or <new.h>
dnl

AC_LANG_CPLUSPLUS
AC_CACHE_CHECK(whether <new> works, ac_cv_good_new_hdr,
AC_TRY_LINK([#include <new>], [
  int a;
  int *b = new(&a) int;
  return 0;
], ac_cv_good_new_hdr=yes, ac_cv_good_new_hdr=no))
if test "$ac_cv_good_new_hdr" = yes; then
   AC_DEFINE(HAVE_NEW_HDR, 1, [Define if <new> exists and works.])
fi
AC_CHECK_HEADERS(new.h)


dnl
dnl functions such as strerror, working strtod, strtoul, time
dnl headers such as unistd.h
dnl

AC_LANG_C
FIXLIBC_O=''
AC_CHECK_FUNCS(strerror, :, FIXLIBC_O='fixlibc.o')

AC_CACHE_CHECK(whether strtod works, ac_cv_working_strtod,
[AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int c, char **v) {
  char s[] = "12 ";
  char *endp;
  double d = strtod(s, &endp);
  exit(*endp == 0 ? 1 : 0);
}], ac_cv_working_strtod=yes, ac_cv_working_strtod=no,
ac_cv_working_strtod=yes)])
if test $ac_cv_working_strtod = no; then 
    FIXLIBC_O='fixlibc.o'
    AC_DEFINE(BROKEN_STRTOD, 1, [Check for bad strtod.])
fi

AC_CHECK_FUNCS(strtoul ctime mkstemp ftruncate)
AC_CHECK_HEADERS(unistd.h fcntl.h)

AC_SUBST(FIXLIBC_O)


dnl
dnl integer types
dnl

AC_CHECK_HEADERS(inttypes.h, have_inttypes_h=yes, have_inttypes_h=no)
AC_CHECK_HEADERS(sys/types.h, have_sys_types_h=yes, have_sys_types_h=no)

if test $have_inttypes_h = no -a $have_sys_types_h = yes; then
    AC_CACHE_CHECK(for uintXX_t typedefs, ac_cv_uint_t,
    [AC_EGREP_HEADER(dnl
changequote(<<,>>)<<(^|[^a-zA-Z_0-9])uint32_t[^a-zA-Z_0-9]>>changequote([,]),
    sys/types.h, ac_cv_uint_t=yes, ac_cv_uint_t=no)])
fi
if test $have_inttypes_h = no -a $have_sys_types_h = yes -a "$ac_cv_uint_t" = no; then
    AC_CACHE_CHECK(for u_intXX_t typedefs, ac_cv_uint_t,
    [AC_EGREP_HEADER(dnl
changequote(<<,>>)<<(^|[^a-zA-Z_0-9])u_int32_t[^a-zA-Z_0-9]>>changequote([,]),
    sys/types.h, ac_cv_u_int_t=yes, ac_cv_u_int_t=no)])
fi
if test $have_inttypes_h = yes -o "$ac_cv_uint_t" = yes; then
    :
elif test "$ac_cv_u_int_t" = yes; then
    AC_DEFINE(HAVE_U_INT_TYPES, 1, [Define if you have u_intXX_t types but not uintXX_t types.])
else
    AC_MSG_ERROR([
=========================================

Neither uint32_t nor u_int32_t defined by <inttypes.h> or <sys/types.h>!

=========================================])
fi


dnl
dnl ntohs, ntohl (need them in C++ code)
dnl

AC_LANG_CPLUSPLUS
AC_CHECK_HEADERS(netinet/in.h sys/param.h)
AC_LANG_C


dnl
dnl set path variables
dnl

shell_expand () {
  val=`eval echo '$'"$1"`
  while echo "x$val" | fgrep '$' >/dev/null 2>&1; do val=`eval echo "$val"`; done
  eval "$1='$val'"
}

dnl Preset $prefix and $exec_prefix.
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

shell_expand datadir

AC_DEFINE_UNQUOTED(SHAREDIR, "$datadir/lcdf-typetools", [Data directory.])


dnl
dnl Output
dnl

AC_OUTPUT(Makefile liblcdf/Makefile libefont/Makefile t1lint/Makefile t1testpage/Makefile cfftot1/Makefile otftopl/Makefile)
