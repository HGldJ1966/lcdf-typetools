dnl Process this file with autoconf to produce a configure script.
AC_INIT(NEWS)
AM_INIT_AUTOMAKE(t1sicle, 0.9)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
if test -n "$GCC" ; then CC="$CC -W -Wall" ; CXX="$CXX -W -Wall" ; fi

AC_PROG_RANLIB

dnl
dnl directory searching
dnl

AC_HEADER_DIRENT

dnl
dnl strerror, working strto[dl], strtoul, time
dnl

FIXLIBC_O=''
AC_CHECK_FUNC(strerror, AC_DEFINE(HAVE_STRERROR), FIXLIBC_O='fixlibc.o')

AC_CACHE_CHECK(whether strtod works, ac_cv_working_strtod,
[AC_TRY_RUN([#include <stdio.h>
#include <stdlib.h>
int main(int c, char **v) {
  char s[] = "12 ";
  char *endp;
  double d = strtod(s, &endp);
  exit(*endp == 0 ? 1 : 0);
}], ac_cv_working_strtod=yes, ac_cv_working_strtod=no,
ac_cv_working_strtod=yes)])
if test $ac_cv_working_strtod = no ;
then FIXLIBC_O='fixlibc.o'; AC_DEFINE_UNQUOTED(BROKEN_STRTOD)
fi

AC_CHECK_FUNCS(strtoul ctime)

AC_SUBST(FIXLIBC_O)

dnl
dnl new.h
dnl

AC_LANG_CPLUSPLUS
AC_CACHE_CHECK(whether <new> works, ac_cv_good_new_hdr,
AC_TRY_LINK([#include <new>], [
  int a;
  int *b = new(&a) int;
  return 0;
], ac_cv_good_new_hdr=yes, ac_cv_good_new_hdr=no))
if test "$ac_cv_good_new_hdr" = yes; then
   AC_DEFINE(HAVE_NEW_HDR)
fi
AC_CHECK_HEADERS(new.h)

dnl
dnl Output
dnl

AC_OUTPUT(Makefile edlib/Makefile libefont/Makefile t1lint/Makefile t1testpage/Makefile cfftot1/Makefile)
